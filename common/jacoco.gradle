apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.10'
}

if (project.name != 'common') {
    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        finalizedBy 'jacocoTestReport'
    }

    // 등록된 적 없는 경우에만 등록
    if (!tasks.names.contains("jacocoTestReport")) {
        tasks.register('jacocoTestReport', JacocoReport) {
            dependsOn test

            reports {
                html.required.set(true)
                xml.required.set(true)
                csv.required.set(false)
            }

            classDirectories.setFrom(
                    fileTree(dir: "$buildDir/classes/java/main") {
                        exclude '**/dto/**', '**/config/**', '**/*Application*', '**/Q*'
                    }
            )

            sourceDirectories.setFrom(files("src/main/java"))
            executionData.setFrom(fileTree(dir: buildDir, includes: [
                    "jacoco/test.exec", "jacoco/test*.exec"
            ]))
        }
    }
}
